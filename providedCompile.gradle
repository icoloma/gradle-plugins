/**
	Add the providedCompile configuration to a regular Java project
 */
configurations {
	// pending http://jira.codehaus.org/browse/GRADLE-471
	providedCompile { description = 'Jars not to be included in the generated POM' }
}

sourceSets {
	main {
		compileClasspath += [configurations.providedCompile]
		runtimeClasspath += [configurations.providedCompile]
	}
	test {
		compileClasspath += [configurations.providedCompile]
		runtimeClasspath += [configurations.providedCompile]
	}
}

// add to idea classpath
// extracted from http://gradle.1045684.n5.nabble.com/Idea-project-generation-and-provided-scope-td4684287.html
idea.module {
  scopes.PROVIDED.plus += [configurations.providedCompile]
  scopes.COMPILE.minus += [configurations.providedCompile]
}

// add to javadoc and eclipse classpath
gradle.taskGraph.beforeTask { task ->
    if (task.name == 'javadoc')
		task.options.classpath = task.options.classpath + configurations.providedCompile.resolve()
    if (task.name == 'eclipseClasspath')
    	task.plusConfigurations += configurations.providedCompile
}
